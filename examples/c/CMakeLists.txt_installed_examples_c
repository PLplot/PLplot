# c/CMakeLists.txt for installed PLplot examples
###
### Process this file with cmake to produce Makefile
###
# Copyright (C) 2009 Alan W. Irwin
#
# This file is part of PLplot.
#
# PLplot is free software; you can redistribute it and/or modify
# it under the terms of the GNU Library General Public License as published
# by the Free Software Foundation; version 2 of the License.
#
# PLplot is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Library General Public License for more details.
#
# You should have received a copy of the GNU Library General Public License
# along with PLplot; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA

set(c_STRING_INDICES
  "01"
  "02"
  "03"
  "04"
  "05"
  "06"
  "07"
  "08"
  "09"
  "10"
  "11"
  "12"
  "13"
  "14"
  "15"
  "16"
  "17"
  "18"
  "19"
  "20"
  "21"
  "22"
  "23"
  "24"
  "25"
  "26"
  "27"
  "28"
  "29"
  "30"
  "31"
  "32"
  )

include_directories(${INCLUDE_DIR})
foreach(STRING_INDEX ${c_STRING_INDICES})
  add_executable(x${STRING_INDEX}c x${STRING_INDEX}c.c)
  target_link_libraries(x${STRING_INDEX}c plplot${LIB_TAG} ${MATH_LIB})
  set_property(GLOBAL APPEND PROPERTY TARGETS_examples_c x${STRING_INDEX}c)
endforeach(STRING_INDEX ${c_STRING_INDICES})

if(ENABLE_gnome2)
  # plplotcanvas_demo.c and plplotcanvas_animation.c both
  # contain "#include <gtk/gtk.h>" which occurs in the
  # /usr/include/gtk-2.0 directory.  They also contain 
  # "#include <plplotcanvas.h>".  These issues are both taken care of
  # by the libgnomeprintui-2.2 pkg-config module.
  pkg_check_pkgconfig(libgnomeprintui-2.2 includedir libdir linkflags1 cflags1 version1 _GNOMEPRINTUI)
  if(linkflags1)
    # Requires blank-delimited list.
    string(REGEX REPLACE ";" " " GNOMEPRINTUI_COMPILE_FLAGS "${cflags1}")
    set(GNOMEPRINTUI_LINK_FLAGS "${linkflags1}")
  else(linkflags1)
    message(STATUS "WARNING: libgnomeprintui-2.2 not found.  plplotcanvas_demo and plplotcanvas_animation not built.")
    set(ENABLE_gnome2 OFF)
  endif(linkflags1)

  # plplotcanvas_animation.c contains a reference to gthread_init and also
  # contains '#include <glib.h>'.  Analysis shows both these issues are
  # taken care of by the gthread-2.0 pkg-config module.
  pkg_check_pkgconfig(gthread-2.0 includedir libdir linkflags2 cflags2 version2 _GTHREAD_2.0)
  if(linkflags2)
    # Requires blank-delimited list.
    string(REGEX REPLACE ";" " " GTHREAD_2.0_COMPILE_FLAGS "${cflags2}")
    set(GTHREAD_2.0_LINK_FLAGS "${linkflags2}")
    set(ENABLE_plplotcanvas_animation ON)
  else(linkflags2)
    message(STATUS "WARNING: gthread-2.0 not found.  plplotcanvas_animation not built")
    set(ENABLE_plplotcanvas_animation OFF)
  endif(linkflags2)
endif(ENABLE_gnome2)
		      
if(ENABLE_gnome2)
  set_source_files_properties(
    plplotcanvas_demo.c
    PROPERTIES COMPILE_FLAGS "${GNOMEPRINTUI_COMPILE_FLAGS}"
    )
  add_executable(plplotcanvas_demo plplotcanvas_demo.c)
  target_link_libraries(plplotcanvas_demo ${GNOMEPRINTUI_LINK_FLAGS} plplotgnome2${LIB_TAG})
  if(ENABLE_plplotcanvas_animation)
    set_source_files_properties(
      plplotcanvas_animation.c
      PROPERTIES COMPILE_FLAGS "${GNOMEPRINTUI_COMPILE_FLAGS} ${GTHREAD_2.0_COMPILE_FLAGS}"
      )
    add_executable(plplotcanvas_animation plplotcanvas_animation.c)
    target_link_libraries(plplotcanvas_animation ${GNOMEPRINTUI_LINK_FLAGS} ${GTHREAD_2.0_LINK_FLAGS} plplotgnome2${LIB_TAG})
  endif(ENABLE_plplotcanvas_animation)
endif(ENABLE_gnome2)
