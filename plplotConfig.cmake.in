@PACKAGE_INIT@

# Users of the python binding of PLplot may require this location.
set(${CMAKE_FIND_PACKAGE_NAME}_PYTHON_SITE_PACKAGES_PREFIX @PYTHON_INSTDIR@)
# Determine Threads::Threads interface library following what is done
# in cmake/modules/threads.cmake.  The value of
# THREADS_PREFER_PTHREAD_FLAG is configured to be the same as used in
# the core build.
find_package(Threads)

if(ANY_QT_DEVICE AND PLPLOT_USE_QT5)
  # Find Qt5 components that we need to link applications against
  # the plplot library (if ENABLE_DYNDRIVERS is false) and which we
  # need in any case if we want to build qt_example and memqt_example.
  # Maintenance 2020-07
  # This should be the same as the Qt5 find_package command in cmake/modules/qt.cmake
  find_package(Qt5 5.7.1 COMPONENTS Svg Gui PrintSupport Widgets)
  if(NOT(Qt5_FOUND AND Qt5_VERSION_MAJOR STREQUAL "${CORE_Qt5_VERSION_MAJOR}" AND Qt5_VERSION_MINOR STREQUAL "${CORE_Qt5_VERSION_MINOR}" AND Qt5_VERSION_PATCH STREQUAL "${CORE_Qt5_VERSION_PATCH}"))
    message(STATUS "CORE_Qt5_FOUND = ON")
    message(STATUS "CORE_Qt5_VERSION_MAJOR = ${CORE_Qt5_VERSION_MAJOR}")
    message(STATUS "CORE_Qt5_VERSION_MINOR = ${CORE_Qt5_VERSION_MINOR}")
    message(STATUS "CORE_Qt5_VERSION_PATCH = ${CORE_Qt5_VERSION_PATCH}")
    message(STATUS "Qt5_FOUND = ${Qt5_FOUND}")
    message(STATUS "Qt5_VERSION_MAJOR = ${Qt5_VERSION_MAJOR}")
    message(STATUS "Qt5_VERSION_MINOR = ${Qt5_VERSION_MINOR}")
    message(STATUS "Qt5_VERSION_PATCH = ${Qt5_VERSION_PATCH}")
    message(STATUS "WARNING: Qt5 core build-tree and install-tree inconsistency")
  endif(NOT(Qt5_FOUND AND Qt5_VERSION_MAJOR STREQUAL "${CORE_Qt5_VERSION_MAJOR}" AND Qt5_VERSION_MINOR STREQUAL "${CORE_Qt5_VERSION_MINOR}" AND Qt5_VERSION_PATCH STREQUAL "${CORE_Qt5_VERSION_PATCH}"))
endif(ANY_QT_DEVICE AND PLPLOT_USE_QT5)

if(PL_HAVE_QHULL)
  # Maintenance(2021)
  # Find qhull the same way as done in cmake/modules/csiro.cmake
  find_package(Qhull CONFIG)
  if(NOT Qhull_FOUND)
    message(STATUS "WARNING: Qhull core build-tree and install-tree inconsistency")
  endif(NOT Qhull_FOUND)
endif(PL_HAVE_QHULL)

# Find export files in same directory location as present file.
include(${CMAKE_CURRENT_LIST_DIR}/plplot_exports.cmake)
